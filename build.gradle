/*
 * Copyright 2021 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
}
project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }
    jcenter()
}

dependencies {
    api 'com.epam.reportportal:client-java:5.0.18'
    api 'com.github.reportportal:commons-model:5.0.0'
    api ("org.jbehave:jbehave-core:${project.jbehave_version}") {
        exclude group: 'org.hamcrest'
        exclude group: 'junit'
    }
    api 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'com.github.reportportal:agent-java-test-utils:ddcf50ee20'
    testImplementation 'org.aspectj:aspectjweaver:1.9.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${project.mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.0.3'
    testImplementation ("org.junit.platform:junit-platform-runner:${project.junit5_launcher_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.platform:junit-platform-launcher:${project.junit5_launcher_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${project.junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${project.junit5_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${project.mockito_version}"
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation ("org.jbehave:jbehave-gherkin:${project.jbehave_version}") {
        exclude group: 'junit'
    }
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    forkEvery(1)
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    testLogging.exceptionFormat = 'full'
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

afterReleaseBuild.dependsOn bintrayUpload
