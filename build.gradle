plugins {
    id 'java'
}
project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    releaseMode ? maven { url 'http://dl.bintray.com/epam/reportportal' } : maven { url 'https://jitpack.io' }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:5.0.0-BETA-7'
        compile 'com.epam.reportportal:commons-model:5.0.0'
    } else {
        compile 'com.github.reportportal:client-java:da4341a'
        compile 'com.github.reportportal:commons-model:5.0.0'
    }
    compile 'org.jbehave:jbehave-core:4.5.1'

    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
}

wrapper {
    gradleVersion = '5.4.1'
}

task writeAgentProperties(type: WriteProperties) {
    outputFile = file("${project.projectDir}/src/main/resources/agent.properties")
    property('agent.name', project.name)
    property('agent.version', project.version)
}

compileJava.dependsOn writeAgentProperties

afterReleaseBuild.dependsOn bintrayUpload

